buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        classpath 'gradle.plugin.org.spongepowered:spongegradle:0.8.1'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.plugin'

minecraft {
    mappings channel: 'snapshot', version: '20180814-1.12'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven { url 'https://jitpack.io' }

    flatDir {
        dirs '../../Libraries/'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
    compileOnly 'org.spongepowered:spongeforge:1.12.2-2838-7.1.8:dev'

    compileOnly project(':forge')
    compileOnly project(':api')

    compileOnly group: 'org.spongepowered', name: 'configurate-yaml', version: '4.0.0'

    compileOnly group: 'com.github.Pixelmon-Development.API', name: 'commons', version: '0.4.7'
    compileOnly group: 'com.github.Pixelmon-Development.API', name: 'forge', version: '0.4.7'
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveName = rootProject.name + '-Sponge-Bridge.jar'

    relocate('org.spongepowered.configurate', 'com.envyful.economies.forge.shade.configurate')
    relocate('org.yaml.snakeyaml', 'com.envyful.economies.forge.shade.snakeyaml')
    relocate('io.leangen.geantyref', 'com.envyful.economies.forge.shade.geantyref')
    relocate('com.google.gson', 'com.envyful.economies.forge.shade.gson')
    relocate('com.zaxxer', 'com.envyful.economies.forge.shade.hikari')
    relocate('org.slf4j', 'com.envyful.economies.forge.shade.slf4j')
    relocate('com.envyful.api', 'com.envyful.economies.forge.shade.envy.api')
    relocate('org.bstats', 'com.envyful.economies.forge.shade.bstats')


    exclude "**/module-info.class"
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

jar.finalizedBy('reobfJar')
build.finalizedBy('versionedReleaseOverride')


task versionedReleaseOverride(type: Copy) {
    group "build"
    from('./build/libs/' + rootProject.name + '-Sponge-Bridge.jar')
    into('../release/')
    include '*.jar'
    rename { String filename ->
        filename.replace(".jar", "-${project.version}.jar")
    }
}